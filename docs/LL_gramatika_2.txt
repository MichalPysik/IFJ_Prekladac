// možná oprava LL gramatiky, pokud nebude fungovat precedenční gramatika
// <expression> se zde chová jako terminál

1. <program>        -> package id EOL <program>
2. <program>        -> func id ( <param_in_first> ) <funkce_body> EOL <program>
3. <program>        -> EOL <program>
4. <program>        -> EOF

5. <param_in_first> -> id <type> <param_in_next>
6. <param_in_first> -> ε
7. <param_in_next>  -> , id <type> <param_in_next>
8. <param_in_next>  -> ε

9. <funkce_body>    -> { EOL <statements> }
10. <funkce_body>   -> ( <type> <param_out_next> ) { EOL <statements> }

11. <param_out_next>-> , <type> <param_out_next>
12. <param_out_next>-> ε



13. <statements>    -> id <statement_id> EOL <statements>
14. <statements>    -> return <expression> <expr_next> EOL <statements>
15. <statements>    -> if <expression> { EOL <statements> } else { EOL <statements> } EOL <statements>
16. <statements>    -> for <for_definition> ; <expression> ; <for_assignment> { EOL <statements> } EOL <statements>
17. <statements>    -> EOL <statements>
18. <statements>    -> ε


19. <statement_id>  -> ( <expr_first> )
20. <statement_id>  -> := <expression_ids>
21. <statement_id>  -> = <expression_ids>
22. <statement_id>  -> , id <id_next>

23. <id_next>       -> , id <id_next>
24. <id_next>       -> := <expression_ids>
25. <id_next>       -> = <expression_ids>


26. <for_definition>-> id <for_def_id>
27. <for_definition>-> ε
28. <for_def_id>    -> , id <for_def_id>
29. <for_def_id>    -> := <expression_ids>

30. <for_assignment>-> id <for_ass_id>
31. <for_assignment>-> ε
32. <for_ass_id>    -> , id <for_ass_id>
33. <for_ass_id>    -> = <expression_ids>



34. <expression_ids>-> id ( <expr_first> )
35. <expression_ids>-> <expression> <expr_next>

36. <expr_first>    -> <expression> <expr_next>
37. <expr_first>    -> ε

38. <expr_next>     -> , <expression> <expr_next>
39. <expr_next>     -> ε



40. <type>          -> int
41. <type>          -> float
42. <type>          -> string
